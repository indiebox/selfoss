developer=http://selfoss.aditu.de/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=2.18
pkgrel=3
pkgdesc="The new multipurpose rss reader, live stream, mashup, aggregation web application"
arch=('any')
license=("GPL3")
source=("https://github.com/SSilence/selfoss/releases/download/${pkgver}/selfoss-${pkgver}.zip")
releasepage=('https://github.com/SSilence/selfoss/releases')
options=('!strip')
sha512sums=('6966458f4ef803ac87df7a2650aed73d6d99922f84d21f64950964b752535d3a1ddd8c585ce2ca5a3f58fb9f2de76fa7a0932ce2eb62faa753ed2398b6ebb748')

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/$pkgname
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/${pkgname}/

# Templates
    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    install -m644 ${startdir}/tmpl/{config.ini,cronjob,fix-trigger-definers.sql}.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl/

# Code
    install -D -m644 ${startdir}/bin/first-visit.pl -t ${pkgdir}/usr/share/${pkgname}/bin/
    cp -dr --no-preserve=ownership ${startdir}/src ${pkgdir}/usr/share/${pkgname}/selfoss
}
