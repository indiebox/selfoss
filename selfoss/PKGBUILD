developer=http://selfoss.aditu.de/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=2.16
pkgrel=1
pkgdesc="The new multipurpose rss reader, live stream, mashup, aggregation web application"
arch=('any')
license=("GPL")
source=("https://github.com/SSilence/selfoss/archive/${pkgver}.tar.gz")
options=('!strip')
sha512sums=('360d42885ff7697d79e6cbcb6dd7b6792c5e9541230be87310e31eccd65430d6c1f47bd42d7035ecd61ea594b7f29a7cd694b1d1ce2a6cfd2284d9edf8277a5d')

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/$pkgname
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/${pkgname}/

# Templates
    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    install -m644 ${startdir}/tmpl/{config.ini,cronjob,fix-trigger-definers.sql}.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl/

# Code
    install -m644 ${startdir}/first-visit.pl ${pkgdir}/usr/share/${pkgname}/
    cp -dr --no-preserve=ownership ${startdir}/src/selfoss-${pkgver} $pkgdir/usr/share/${pkgname}/selfoss
}
