developer=http://selfoss.aditu.de/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=2.18
pkgrel=1
pkgdesc="The new multipurpose rss reader, live stream, mashup, aggregation web application"
arch=('any')
license=("GPL3")
# source=("https://github.com/SSilence/selfoss/releases/download/${pkgver}/selfoss-${pkgver}.zip")
_buildid=77d57d1
# For the time being, PHP 7.2 is not officially supported yet
source=("selfoss-${pkgver}-${_buildid}.zip::https://bintray.com/fossar/selfoss/download_file?file_path=selfoss-${pkgver}-${_buildid}.zip")
releasepage=('https://github.com/SSilence/selfoss/releases')
options=('!strip')
sha512sums=('de6eea8e1814f4da3d24c55390c65cad1a3df1458a69ec391f2b915518bef83bcb6f3fb37ab03368670101dd715444f1f670c7086dc79b81599484a7125979d8')

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/$pkgname
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/${pkgname}/

# Templates
    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    install -m644 ${startdir}/tmpl/{config.ini,cronjob,fix-trigger-definers.sql}.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl/

# Code
    install -D -m644 ${startdir}/bin/first-visit.pl -t ${pkgdir}/usr/share/${pkgname}/bin/
    cp -dr --no-preserve=ownership ${startdir}/src ${pkgdir}/usr/share/${pkgname}/selfoss
}
